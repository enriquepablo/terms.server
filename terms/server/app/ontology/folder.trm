# Copyright (c) 2007-2012 by Enrique PÃ©rez Arnaud <enriquepablo@gmail.com>
#
# This file is part of the terms project.
# https://github.com/enriquepablo/terms
#
# The terms project is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# The terms project is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with any part of the terms project.
# If not, see <http://www.gnu.org/licenses/>.
<https://raw.github.com/enriquepablo/terms.server/master/terms/server/app/ontology/folder.trm>
{{{
"author": "enrique perez arnaud"
}}}

import <https://raw.github.com/enriquepablo/terms.server/master/terms/server/app/ontology/owning.trm>.


a folder is a content.

to is-placed is to unique, subj a content, in a folder.

to put-in-folder is to abs-person-action, what a content, in a folder.

(wants Person1, do (put-in-folder Person1, what Content1, in Folder1));
(located Folder1, in Context1);
(has-role Person1, which Role1, where Context1);
(has-permission Role1, which add-perm);
(is-owned Content1, by Person1)
->
(is-placed Content1, in Folder1).

to remove-from-folder is to abs-person-action, what a content, from a folder.

(wants Person1, do (remove-from-folder Person1, what Content1, from Folder1));
(located Folder1, in Context1);
(has-role Person1, which Role1, where Context1);
(has-permission Role1, which manage-perm)
->
(finish Person1, what (is-placed Content1, in Folder1)).

to move-to-folder is to abs-person-action, what a content, from a folder, into a folder.

(wants Person1, do (move-to-folder Person1, what Content1, from Folder1, into Folder2));
(located Folder1, in Context1);
(located Folder2, in Context1);
(has-role Person1, which Role1, where Context1);
(has-permission Role1, which move-perm)
->
(is-placed Content1, in Folder2).


to list-folder is to abs-content-action, what a folder.

(is-protected list-folder, by edit-perm, for private).
(is-protected list-folder, by review-perm, for pending).
(is-protected list-folder, by basic-perm, for public).
