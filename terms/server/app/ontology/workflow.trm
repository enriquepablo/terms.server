# Copyright (c) 2007-2012 by Enrique PÃ©rez Arnaud <enriquepablo@gmail.com>
#
# This file is part of the terms project.
# https://github.com/enriquepablo/terms
#
# The terms project is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# The terms project is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with any part of the terms project.
# If not, see <http://www.gnu.org/licenses/>.

<https://raw.github.com/enriquepablo/terms.server/master/terms/server/app/ontology/workflow.trm>
{{{
"author": "enrique perez arnaud"
}}}

import <https://raw.github.com/enriquepablo/terms.server/master/terms/server/app/ontology/content.trm>.

a workflow is a thing.

to is-assigned is to endure, subj a workflow, for a noun, in a context.

a role is a thing.
to has-role is to endure, subj a group, which a role, where a context.

a permission is a thing.
to has-permission is to endure, subj a role, which a permission.

manager is a role.

manage-perm is a permission.
(has-permission manager, which manage-perm).

add-perm is a permission.
(has-permission manager, which add-perm).

(has-role admin, which manager, where global).

a status is a thing.
to has-status is to exclusive-endure, subj a content, which a status.

to is-protected is to endure, subj a verb, by a permission, for a status.

(want Person1, do Abs-content-action1:(Abs-content-actionVerb1 Person1, what Content1));
# (is-protected Abs-content-actionVerb2, by Permission1, for Status1);
(is-protected Abs-content-actionVerb1, by Permission1, for Status1);
(belong Person1, where Group1);
(has-role Group1, which Role1, where Context1);
(has-permission Role1, which Permission1);
(has-status Content1, which Status1);
(located Content1, in Context1)
# a Abs-content-actionVerb1 is a Abs-content-actionVerb2
->
(Abs-content-action1).

a transition is a thing.

to has-transition is to endure, subj a workflow, which a transition.

to abs-wf-action is to abs-content-action.

to executed is to endure, subj a transition, by a verb, from a status, towards a status.

(Abs-wf-actionVerb1 Person1, what Content1);
(is-assigned Workflow1, for ContentNoun1, in Context1);
(has-transition Workflow1, which Transition1);
(executed Transition1, by Abs-wf-actionVerb1, from Status1, towards Status2);
(has-status Content1, which Status1);
(located Content1, in Context1);
Content1 is a ContentNoun1
->
(has-status Content1, which Status2).


to grant-role is to abs-person-action, which a role, for a person, in a context.

(want Person1, do (grant-role Person1, which Role1, in Context1, for Person2));
(has-role Person1, which Role2, where Context1);
(has-permission Role2, which manage-perm)
->
(has-role Person2, which Role1, where Context1).

(initialize Person1, obj Context1)
->
(located Context1, in global);
(has-role Person1, which manager, where Context1).


(initialize Person1, obj Person2)
->
(has-role Person2, which manager, where Person2).
